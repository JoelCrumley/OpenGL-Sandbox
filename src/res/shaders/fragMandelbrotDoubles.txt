#version 400

#define CROSSHAIR_WIDTH 1

uniform dvec2 u_resolution;
uniform dvec2 translation;
uniform double zoom;
uniform float limit;
uniform int iterations;
uniform int colourMode;
uniform bool showCrosshair;

out vec4 out_Color;

dvec2 mandelbrot(dvec2 point, dvec2 c) {
    return dvec2(point.x*point.x-point.y*point.y+c.x, 2.0*point.x*point.y+c.y);
}

double magSquared(dvec2 point) {
    return point.x*point.x + point.y*point.y;
}

vec3 hsbToRgb(in dvec3 col){
    vec3 c = vec3(col);
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix(vec3(1.0), rgb, c.y);
}

void main(void){

    dvec2 st = dvec2(gl_FragCoord.xy)/u_resolution.xy;
	// out_Color = vec4(st, 0.0, 1.0);

    st *= 2.0; // map bounds 0.0 -> 1.0 to 0.0 -> 2.0
    st -= dvec2(1.0, 1.0); // map bounds 0.0 -> 2.0 to -1.0 -> 1.0
    st *= zoom; // map bounds -1.0 -> 1.0 to -zoom -> +zoom
    st += translation; // map (0.0, 0.0) to (translation.xy)

    dvec2 point = dvec2(st);

    double limitDouble = double(limit);

    int broke = -1;
    for (int i = 0; i < iterations; i++) {
    	if (magSquared(point) >= limitDouble*limitDouble) {
            broke = i;
            break;
        } else {
            point = mandelbrot(point, st);
        }
    }

    // our "convergence" amount to convert to a colour:
    //double metric = (limitDouble-double(broke)) / limitDouble;
    double metric = (double(iterations)-double(broke)) / double(iterations);

    if (colourMode == 1) {
        gl_FragColor = vec4(0.0,0.0,broke == -1 ? 0.0 : metric, 1.0);
    } else if (colourMode == 2) {
        gl_FragColor = vec4(0.0,0.0,broke == -1 ? 0.0 : floor(10.0 * ( metric ) ) / 10.0, 1.0);
    } else if (colourMode == 3) {
        gl_FragColor = vec4(broke == -1 ? vec3(0.0,0.0,0.0) : hsbToRgb(dvec3(1.0 - metric, 1.0, 1.0)), 1.0);
    } else {
        gl_FragColor = vec4(vec3(broke == -1 ? 0.0 : metric), 1.0);
    }

    if (showCrosshair) {
        int centerX = int(u_resolution.x/2.0);
        int centerY = int(u_resolution.y/2.0);
        if (((int(gl_FragCoord.x) >= centerX - CROSSHAIR_WIDTH) && (int(gl_FragCoord.x) <= centerX + CROSSHAIR_WIDTH) && (int(gl_FragCoord.y) >= centerY - CROSSHAIR_WIDTH) && (int(gl_FragCoord.y) <= centerY + CROSSHAIR_WIDTH))) {
            gl_FragColor = vec4(1.0 - gl_FragColor.x, 1.0 - gl_FragColor.y, 1.0 - gl_FragColor.z, 1.0);
        }
    }

}