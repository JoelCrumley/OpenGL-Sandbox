#version 400

uniform vec2 resolution;
uniform vec2 translation;
uniform float zoom;

// "Coordinate length" * pixelScale = "number of pixels that length appears as on screen".
uniform float pixelScale;

out vec4 out_Color;

vec4 getColour(vec2 coordinate) {
    vec2 v = vec2(abs(mod(coordinate.x, 1.0) - 0.5), abs(mod(coordinate.y, 1.0) - 0.5));
    float d = max(v.x, v.y);

    // 0.5 - d = how far coordinate is to a grid line in coordinate space
    // pixelScale * (0.5 - d) = how far coordinate is to a grid line IN SCREEN PIXELS

    // 1.0 - step(pixelScale * (0.5 - d), 2) = ...
    // 1.0 (white) if coordinate is more than 2 pixels from a grid line, 0.0 (black) if coordinate is less than 2 pixels from a grid line
    d = smoothstep(0.5, 1.5 ,pixelScale * (0.5 - d));

    // Converting number from [0.0, 1.0] to [0.5, 0.9]
    d = 0.5 + 0.4 * d;

    return vec4(d, d, d, 1.0);
}

void main(void){

    // frag coord is pixel coord e.g. could be (1410, 241)
    // resolution is in pixels e.g. (1920, 1080)

    vec2 st = gl_FragCoord.xy/resolution.xy;

    float aspectRatio = resolution.x / resolution.y;
    // aspect ratio E (1, inf] => width > height
    // aspect ratio E (0, 1) => width < height

    st *= 2.0; // Mapping [0, 1] x [0, 1] space to [0, 2] x [0, 2] space.
    st -= vec2(1.0, 1.0); // Re-centering origin, moving to [-1, 1] x [-1, 1] space.

    st *= zoom; // Zoom in, zoom=2.0 would zoom out by 2x, zoom=0.5 would zoom in by 2x.
    // zoom = 2.0 case: [-1, 1] maps to [-2, 2] so objects get 2x smaller.

    st.x *= aspectRatio; // Fix distortion from mapping rectangle (screen) to square ( [0,1]x[0,1] )
    st += translation; // Move origin to point of interest.


	out_Color = getColour(st);

}